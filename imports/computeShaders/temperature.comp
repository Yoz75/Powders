#version 430

// Must be Map.chunkSize
layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

struct Temperature
{
    float heatCapacity;
    float transferCoefficient;
    float value;
};

uniform ivec2 resolution;

layout(std430, binding = 1) readonly restrict buffer temperatureInBuffer
{
    Temperature inValues[];
};

layout(std430, binding = 2) writeonly restrict buffer temperatureOutBuffer
{
    Temperature outValues[];
};

void main()
{
    uint x = gl_GlobalInvocationID.x;
    uint y = gl_GlobalInvocationID.y;

    uint selfIndex = x + resolution.x * y;

    Temperature self = inValues[selfIndex];

    float delta = 0;

    for(int biasY = -1; biasY < 2; biasY++)
    {
        for(int biasX = -1; biasX < 2; biasX++)
        {
            if(biasX == 0 && biasY == 0) continue;
            int nx = int(x) + biasX;
            int ny = int(y) + biasY;

            if(nx < 0 || ny < 0 || nx >= resolution.x || ny >= resolution.y) continue;

            int neighborIndex = nx + resolution.x * ny;

            Temperature neighbor = inValues[neighborIndex];

            //(c1*t1 + c2*t2) / (c1 + c2)
            float resultValue = (self.heatCapacity * self.value + neighbor.heatCapacity * neighbor.value) / (self.heatCapacity + neighbor.heatCapacity);

            delta += (resultValue - self.value) * self.transferCoefficient;
        }
    }

    delta /= 8;
    self.value += delta;
    outValues[selfIndex] = self;
}